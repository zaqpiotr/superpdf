plugins {
    id 'java-library'
    id 'groovy'
    id 'com.vanniktech.maven.publish' version '0.28.0'
}

wrapper {
    gradleVersion = '8.10.2'
}

java {
    sourceCompatibility = JavaVersion.VERSION_23
    targetCompatibility = JavaVersion.VERSION_23
}

group = 'com.monitglass'
/*
 * Gets the version name from the latest Git tag
 */
def getVersionName() {
    def proc = ['git', 'describe', '--tags'].execute()
    proc.waitFor()
    def result = proc.text.trim()
    return result.startsWith('v') ? result.substring(1) : result
}

version = getVersionName()

repositories {
    mavenCentral()
}

dependencies {
    api 'org.apache.pdfbox:pdfbox:3.0.6'
    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation 'org.apache.commons:commons-csv:1.9.0'
    implementation group: 'org.jsoup', name: 'jsoup', version: '1.15.3'

    testImplementation 'org.spockframework:spock-core:2.4-M7-groovy-5.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'commons-io:commons-io:2.11.0'
}

test {
    useJUnitPlatform()
}

mavenPublishing {
    publishToMavenCentral(com.vanniktech.maven.publish.SonatypeHost.CENTRAL_PORTAL, true)
    signAllPublications()

    pom {
        name = 'SuperPDF'
        description = 'A Java library to build tables in PDF documents'
        url = 'https://github.com/zaqpiotr/superpdf'
        inceptionYear = '2025'

        licenses {
            license {
                name = 'Apache License, Version 2.0'
                url = 'https://www.apache.org/licenses/LICENSE-2.0'
            }
        }

        developers {
            developer {
                id = 'zaqpiotr'
                name = 'Piotr Zak'
                email = 'zaqpiotr@gmail.com'
                url = 'https://github.com/zaqpiotr'
            }
        }

        scm {
            url = 'https://github.com/zaqpiotr/superpdf'
            connection = 'scm:git:git://github.com/zaqpiotr/superpdf.git'
            developerConnection = 'scm:git:ssh://git@github.com/zaqpiotr/superpdf.git'
        }
    }
}
